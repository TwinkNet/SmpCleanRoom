plugins {
    id 'java'
    id("xyz.beengoo.run-folia") version "0.1.0"
    id("com.diffplug.spotless") version "8.1.0"
    id 'com.gradleup.shadow' version '9.3.1'
}

group = 'network.twink'
version = 'RC-2026.01.26'

repositories {
    mavenCentral()
    maven {
        name = "papermc-repo"
        url = "https://repo.papermc.io/repository/maven-public/"
    }
}

spotless {
    java {
        it.targetExclude("src/main/java/de/**")
        palantirJavaFormat()
        removeUnusedImports()
    }
}

dependencies {
    compileOnly("dev.folia:folia-api:1.21.8-R0.1-SNAPSHOT")
    compileOnly 'net.dmulloy2:ProtocolLib:5.4.0'
    implementation project(':smpcleanroom-api')
}

runFolia {
    mcVersion.set("1.21.4")           // Folia server version to run
    runDir.set(layout.projectDirectory.dir("run")) // Server run directory
    jvmArgs.addAll("-Xmx8G", "-Xms8G") // JVM args
    serverArgs.add("--nogui")        // Server launch arguments
    agreeEula.set(true)              // Auto-accept EULA
}

def targetJavaVersion = 21
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'

    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release.set(targetJavaVersion)
    }
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
}
